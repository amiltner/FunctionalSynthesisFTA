type list = mu list .
  | Nil
  | Cons of (nat * list)

struct
  type t = list

  let empty = Nil ;;

  let size =
    fix (size : t -> nat) =
      fun (l : t) ->
        match l binding l with
        | Nil -> O
        | Cons -> S (size l.1)
  ;;

  let lookup =
    fix (lookup : t -> nat -> bool) =
      fun (l : t) -> fun (x : nat) ->
        match l binding l with
        | Nil -> False
        | Cons ->
          (match nat_eq l.0 x binding c with
           | True -> True
           | False -> lookup l.1 x)
  ;;

  let insert =
    fun (l : t) ->
      fun (x : nat) ->
        match lookup l x binding b with
        | True -> l
        | False -> Cons(x,l)
  ;;

  let delete =
    fix (delete : t -> nat -> t) =
      fun (l : t) -> fun (x : nat) ->
          match l binding lp with
          | Nil -> Nil
          | Cons ->
            (match nat_eq lp.0 x binding c with
             | True -> lp.1
             | False -> Cons(lp.0, delete lp.1 x))
  ;;

  let map =
    fix (map : (nat -> nat) -> t -> t) =
      fun (f : nat -> nat) ->
        fun (l : t) ->
          match l binding l with
          | Nil -> Nil
          | Cons -> insert (map f l.1) (f l.0)
  ;;

  let fold =
    fun (f : nat -> t -> t) ->
      fun (init : t) ->
        ((fix (perform_fold : t -> t -> t) =
          fun (acc : t) ->
            fun (tr : t) ->
              (match tr binding tr with
              | Nil -> acc
              | Cons -> perform_fold (f tr.0 acc) tr.1))
        init)
  ;;
end
:
sig
  type t

  val empty : t

  val size : t -> nat
  val lookup : t -> nat -> bool

  val insert : t -> nat -> t
  val delete : t -> nat -> t

  val map : (nat -> nat) -> t -> t
  val fold : (nat -> t -> t) -> t -> t -> t
end

maintains

forall (s : t) .
  forall (i : nat) .
    forall (j : nat) .
      (and
         (and
            (and
               (nat_eq (O) (size empty))
               (implies
                  (not (lookup s i))
                  (nat_lt (size s)
                          (size (insert s i)))))
            (and
               (not (lookup empty i))
               (lookup (insert s i) i)))
         (and
            (not (lookup (delete s i) i))
            (implies
               (and
                  (not (nat_eq i j))
                  (lookup s j))
               (and
                 (lookup (insert s i) j)
                 (lookup (delete s i) j)))))